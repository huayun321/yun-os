;主引导
;--------------------------------------------------
section MBR vstart=0x7c00
    mov ax, cs
    mov ds, ax
    mov es, ax
    mov ss, ax
    mov fs, ax
    mov sp, 0x7c00

;清屏
;--------------------------------------------------
;INT 0x10 功能号：0x06 功能描述：上卷窗口
;--------------------------------------------------
; 输入：
; AH 功能号=0x06
; AL = 上卷的行数（如果为0 ，表示全部）
; BH = 上卷行属性
; (CL,CH) = 窗口左上角（x,y）位置
; (DL,DH) = 窗口右下角（x,y）位置
; 无返回值
    mov ax, 0x600
    mov bx, 0x700
    mov cx, 0
    mov dx, 0x184f

    int 0x10

;;;;;;;; 下面三行获取光标位置 ;;;;;;;;
; .get_cursor 获取当前光标位置，在光标位置处打印字符。
    mov ah, 3           ;3号子功能
    mov bh, 0           ;bh待获取光标页号

    int 0x10
;;;;;;;; 获取光标位置结束 ;;;;;;;;

;;;;;;;; 打印字符 ;;;;;;;;
    mov ax, message
    mov bp, ax          ;串首地址

    mov cx, 5           ;串长度
    mov ax, 0x1301      ;13显示字符及属性，01显示字符光标跟随移动

    mov bx, 0x2         ;黑底绿字

    int 0x10
;;;;;;;; 打印字符结束 ;;;;;;;;

    jmp $               ;悬停此处

    message db "1 MBR"
    times 510-($-$$) db 0
    db 0x55,0xaa